#!/usr/bin/perl

# i-MSCP - internet Multi Server Control Panel
# Copyright (C) 2010-2014 by internet Multi Server Control Panel
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
#
# @category    i-MSCP
# @copyright   2010-2014 by i-MSCP | http://i-mscp.net
# @author      Daniel Andreca <sci2tech@gmail.com>
# @author      Laurent Declercq <l.declercq@nuxwin.com>
# @link        http://i-mscp.net i-MSCP Home Site
# @license     http://www.gnu.org/licenses/gpl-2.0.html GPL v2

use strict;
use warnings;

use FindBin;
use lib "$FindBin::Bin/..", "$FindBin::Bin/../PerlLib", "$FindBin::Bin/../PerlVendor";

umask(027);

use iMSCP::Debug;
use iMSCP::Bootstrapper;
use iMSCP::Dialog;
use iMSCP::HooksManager;
use iMSCP::Getopt;

# Turn off localisation features to force any command output to be in english
$ENV{'LC_MESSAGES'} = 'C';

newDebug('imscp-setup.log');

silent(1);

$main::execmode = 'setup' unless $main::execmode;

iMSCP::Getopt->parse('Usage: perl imscp-setup [options]');

# Get reconfigure option
$main::reconfigure = iMSCP::Getopt->reconfigure;

# Global variable that holds questions
%main::questions = () unless %main::questions;

# Handle preseed option
my $preseedFile = iMSCP::Getopt->preseed;
if($preseedFile) {
	require $preseedFile;
	# Values from preseed file always override those already set.
	# The preseed option is not compatible with the reconfigure option.
	$main::reconfigure = 'none';
	undef $preseedFile;
}

# handle noprompt option
$main::noprompt = iMSCP::Getopt->noprompt;
debugRegisterCallBack(
	sub {
		if($?) { # We exit with status 5 from iMSCP::Dialog::Dialog in noninteractive mode
			if($? == 5) {
				if(iMSCP::Getopt->preseed) {
					print STDERR output('Noninteractive mode: Missing or bad entry found in your preseed file.', 'fatal');
				} else {
					print STDERR output('Noninteractive mode: Missing or bad entry found in configuration file.', 'fatal');
				}
			}
			fatal(iMSCP::Debug::getLastError());
			exit $?;
		}
		print STDOUT output("i-MSCP $main::imscpConfig{'Version'} has been successfully installed/updated", 'ok');
		exit 0;
	}
) if $main::noprompt;

print STDOUT output("Installation in progress... Please wait.") if $main::noprompt;

require "$FindBin::Bin/imscp-setup-methods.pl";

# Start setup
sub setupStart
{
	# Bootstrap setup
	my $rs = setupBoot();
	return $rs if $rs;

	# Handle the hook-file option
	my $hookFile = iMSCP::Getopt->hookFile;

	if($hookFile) {
		require $hookFile;
	}

	# Allow any server/package to register their setup hook functions
	setupRegisterHooks();
}

# Process both setup dialog and tasks
sub setupProcess
{
	my $rs = iMSCP::HooksManager->getInstance()->trigger('beforeSetup');

	if(! $main::noprompt) {
		my $distribution = ucfirst(lc(iMSCP::LsbRelease->getInstance()->getId(1)));
		my $dialog = iMSCP::Dialog->factory();

		$dialog->set('yes-label', 'Continue');
		$dialog->set('no-label', 'Abort');

		if ($dialog->yesno(<<EOF)) {

Welcome to the \\Z1i-MSCP $main::imscpConfig{'Version'}\\Zn setup dialog.

This program will install/update i-MSCP on your system.

\\Z3Warning:\\Zn Make sure you have read and performed all steps from the
         \\Zbdocs/$distribution/INSTALL\\Zn file.

   \\Z4Note:\\Zn During the setup process some or all services might require
         to be shutdown or restarted. Only services that are not set as
         '\\Zbno\\Zn' in your imscp.conf file will be processed by this program.
EOF

			$dialog->msgbox("\nSetup process has been aborted...");
			exit 0;
		}
	}

	# Process setup dialog
	$rs = setupDialog();
	return $rs if $rs;

	# Process setup tasks
	$rs = setupTasks();
	return $rs if $rs;

	iMSCP::HooksManager->getInstance()->trigger('afterSetup');
}

sub setupShutDown
{
	return 0 if $main::noprompt;

	iMSCP::Dialog->factory()->msgbox(<<EOF);

\\Z1Congratulation\\Zn

i-MSCP has been successfully updated.

Thank you for choosing i-MSCP.
EOF

	0;
}

my $rs = 0;

$rs = setupStart();
$rs ||= setupProcess();
$rs ||= setupShutDown();

exit $rs;
